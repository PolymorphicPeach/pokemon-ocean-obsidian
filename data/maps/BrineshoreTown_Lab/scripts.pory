mapscripts BrineshoreTown_Lab_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: BrineshoreTown_Lab_OnTransition
}

script BrineshoreTown_Lab_OnTransition{
    if(flag(FLAG_SYS_POKEMON_GET)){
        setflag(FLAG_TEMP_1)
    }
    else{
        clearflag(FLAG_TEMP_1)
    }
}

script BrineshoreTown_Lab_EventScript_Pokeball_1{
    lockall
    if(!flag(FLAG_SYS_POKEMON_GET)){
        setvar(VAR_TEMP_0, SPECIES_HONEDGE)
        setvar(VAR_TEMP_1, 1)
        bufferspeciesname(STR_VAR_1, SPECIES_HONEDGE)
        call(BrineshoreTown_Lab_EventScript_ExamineStarter)
    }
    else{
        msgbox(CantTakeAnother)
        waitmessage
    }
    releaseall
    end
}

script BrineshoreTown_Lab_EventScript_Pokeball_2{
    lockall
   if(!flag(FLAG_SYS_POKEMON_GET)){
        setvar(VAR_TEMP_0, SPECIES_TOGEPI)
        setvar(VAR_TEMP_1, 2)
        bufferspeciesname(STR_VAR_1, SPECIES_TOGEPI)
        call(BrineshoreTown_Lab_EventScript_ExamineStarter)
    }
    else{
        msgbox(CantTakeAnother)
        waitmessage
    }
    releaseall
    end
}

script BrineshoreTown_Lab_EventScript_Pokeball_3{
    lockall
    if(!flag(FLAG_SYS_POKEMON_GET)){
        setvar(VAR_TEMP_0, SPECIES_SANDILE)
        setvar(VAR_TEMP_1, 3)
        bufferspeciesname(STR_VAR_1, SPECIES_SANDILE)
        call(BrineshoreTown_Lab_EventScript_ExamineStarter)
    }
    else{
        msgbox(CantTakeAnother)
        waitmessage
    }
    releaseall
    end
}

script BrineshoreTown_Lab_EventScript_Computer{
    lockall
    if(!flag(FLAG_SYS_POKEMON_GET)){
        setvar(VAR_TEMP_0, SPECIES_PORYGON)
        setvar(VAR_TEMP_1, 4)
        bufferspeciesname(STR_VAR_1, SPECIES_PORYGON)
        call(BrineshoreTown_Lab_EventScript_ExamineStarter)
    }
    else{
        msgbox(CantTakeAnother)
        waitmessage
    }
    releaseall
    end
}

script BrineshoreTown_Lab_EventScript_ExamineStarter{
    showmonpic(VAR_TEMP_0, 10, 3)
    msgbox(ChooseStarterText, MSGBOX_YESNO)

    if(var(VAR_RESULT) == YES){
        playfanfare(MUS_OBTAIN_ITEM)
        // give pokemon

        if(var(VAR_TEMP_0) == SPECIES_TOGEPI){
            givemon(VAR_TEMP_0, 5, ITEM_NONE, ITEM_POKE_BALL, 15, 1)
        }
        elif(var(VAR_TEMP_0) == SPECIES_HONEDGE){
            givemon(VAR_TEMP_0, 5, ITEM_NONE, ITEM_POKE_BALL, 3)
        }
        elif(var(VAR_TEMP_0) == SPECIES_SANDILE){
            givemon(VAR_TEMP_0, 5, ITEM_NONE, ITEM_POKE_BALL, 3, 1)
        }
        elif(var(VAR_TEMP_0) == SPECIES_PORYGON){
            givemon(VAR_TEMP_0, 5, ITEM_NONE, ITEM_POKE_BALL, 15, 1)
        }

        // This flag must be set after getting a pokemon 
        // for the first time, or you can't access the 
        // party screen.
        setflag(FLAG_SYS_POKEMON_GET)
        setvar(VAR_STARTER_MON, VAR_TEMP_1)
        setvar(BRINESHORETOWN_STATE, 1)

        // remove starter pokeball
        call(BrineshoreTown_Lab_RemoveStarterPokeball)
        msgbox(StarterChosenText)
        waitmessage
    }
    else{
        msgbox(StillLookingText)
        waitmessage
    }
    hidemonpic
}

script BrineshoreTown_Lab_RemoveStarterPokeball{
    removeobject(VAR_TEMP_1)

    if(var(VAR_TEMP_1) == 1){
        setflag(FLAG_STARTER_BALL_1)
        return
    }
    if(var(VAR_TEMP_1) == 2){
        setflag(FLAG_STARTER_BALL_2)
        return
    }
    if(var(VAR_TEMP_1) == 3){
        setflag(FLAG_STARTER_BALL_3)
        return
    }
    if(var(VAR_TEMP_1) == 4){
        setflag(FLAG_STARTER_BALL_4)
        return
    }
}

script BrineshoreTown_Lab_TalkToYew{
    if(flag(FLAG_STARTER_BALL_4)){
        msgbox("{COLOR BLUE}{SHADOW LIGHT_BLUE}Assistant Yew: {COLOR DARK_GRAY}Hey, the computer is\nworking again!", MSGBOX_NPC)
    }
    else{
        msgbox("{COLOR BLUE}{SHADOW LIGHT_BLUE}Assistant Yew: {COLOR DARK_GRAY}Ugh, this computer is\nalways crashing.\lI think it's that Porygon...", MSGBOX_NPC)
    }
}

script BrineshoreTown_Lab_LeaveLeftTrigger{
    lockall

    if(var(BRINESHORETOWN_STATE) == 1 && !flag(FLAG_SYS_COMPANION_GET)){
        msgbox(WaitText)
        applymovement(OBJ_EVENT_ID_PLAYER, face_up)
        applymovement(5, ProfessorWalkLeftTrigger)
        waitmovement(5)

        call(YewJoinsLeft)

        setflag(FLAG_SYS_COMPANION_GET)
        setvar(BRINESHORETOWN_STATE, 3)
        setflag(FLAG_TEMP_1)
    }

    releaseall
}

script BrineshoreTown_Lab_LeaveRightTrigger{
    lockall

    if(var(BRINESHORETOWN_STATE) == 1 && !flag(FLAG_SYS_COMPANION_GET)){
        msgbox(WaitText)
        applymovement(OBJ_EVENT_ID_PLAYER, face_up)
        applymovement(5, ProfessorWalkRightTrigger)
        waitmovement(5)

        call(YewJoinsRight)

        setflag(FLAG_SYS_COMPANION_GET)
        setvar(BRINESHORETOWN_STATE, 3)
        removeobject(6)
        setflag(FLAG_TEMP_1)

    }

    releaseall
}

script YewJoinsLeft{
    msgbox(TakeYew)
    applymovement(6, YewWalkLeftTrigger)
    waitmovement(6)
    call(YewGivePokedexAndEevee)
    applymovement(6, walk_down)
    waitmovement(6)
    removeobject(6)
}

script YewJoinsRight{
    msgbox(TakeYew)
    applymovement(6, YewWalkRightTrigger)
    waitmovement(6)
    applymovement(OBJ_EVENT_ID_PLAYER, face_right)
    waitmovement(0)
    call(YewGivePokedexAndEevee)
    applymovement(6, walk_left)
    waitmovement(6)
    removeobject(6)
}

script YewGivePokedexAndEevee{
    setvar(VAR_TEMP_2, 5)

    msgbox(GivePokedex)
    waitmessage
    playfanfare(MUS_OBTAIN_ITEM)
    msgbox("{COLOR BLUE}{SHADOW LIGHT_BLUE}You got a Pokedex!", MSGBOX_AUTOCLOSE)
    showmonpic(SPECIES_EEVEE, 10, 3)
    msgbox("{COLOR BLUE}{SHADOW LIGHT_BLUE}You got an Eevee!", MSGBOX_AUTOCLOSE)
    givemon(SPECIES_EEVEE, 5, ITEM_NONE, ITEM_POKE_BALL, VAR_TEMP_2, 1)
    hidemonpic
    
}

script BrineshoreTown_Lab_TalkToProfessor{
    lock
    faceplayer
    if(!flag(FLAG_SYS_POKEMON_GET)){
        msgbox(StandardProfessorText)
        waitmessage
    }
    else{
        msgbox("{COLOR RED}{SHADOW LIGHT_RED}Professor: {COLOR DARK_GRAY}{SHADOW LIGHT_GRAY}Good luck.")
        waitmessage
    }
    release
}



movement YewWalkToProfessor{
    walk_fast_left * 3
    face_down
}

movement ProfessorWalkLeftTrigger{
    walk_right * 2
    walk_down * 2
}

movement ProfessorWalkRightTrigger{
    walk_right * 3
    walk_down * 2
}

movement YewWalkLeftTrigger{
    walk_down
    walk_left * 3
}

movement YewWalkRightTrigger{
    walk_down
    walk_left * 2
}

movement walk_left{
    walk_left
}

movement walk_up{
    walk_up
}

text StandardProfessorText{
    "{COLOR RED}{SHADOW LIGHT_RED}Professor: {COLOR DARK_GRAY}{SHADOW LIGHT_GRAY}Go ahead and choose a Pokemon,\n"
    "I need to get this delivery sorted."
}

text ChooseStarterText{
    "{COLOR RED}{SHADOW LIGHT_RED}Professor: {COLOR DARK_GRAY}{SHADOW LIGHT_GRAY}{PLAYER}, is this {STR_VAR_1} the\n"
    "one you want?\p"
}

text StarterChosenText{
    "{COLOR RED}{SHADOW LIGHT_RED}Professor: {COLOR DARK_GRAY}{SHADOW LIGHT_GRAY}An excellent choice."
}

text StillLookingText{
    "{COLOR RED}{SHADOW LIGHT_RED}Professor: {COLOR DARK_GRAY}{SHADOW LIGHT_GRAY}Ok, {PLAYER}, take your time.\p"
}

text CantTakeAnother{
     "{COLOR RED}{SHADOW LIGHT_RED}Professor: {COLOR DARK_GRAY}{SHADOW LIGHT_GRAY}You can't have that,\n"
     "stop being greedy!"
}

text WaitText{
    "{COLOR RED}{SHADOW LIGHT_RED}Professor: {COLOR DARK_GRAY}{SHADOW LIGHT_GRAY}WAIT!\p"
}

text TakeYew{
    "{COLOR RED}{SHADOW LIGHT_RED}Professor: {COLOR DARK_GRAY}{SHADOW LIGHT_GRAY}Assistant Yew needs you\n"
    "to take him north to the academy."
 
}

text GivePokedex{
     "{COLOR BLUE}{SHADOW LIGHT_BLUE}Assistant Yew: {COLOR DARK_GRAY}{SHADOW LIGHT_GRAY}Thank you for helping!\n"
     "Here, consider it a form of payment."
}