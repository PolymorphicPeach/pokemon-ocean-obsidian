mapscripts BrineshoreTown_Rathouse_Basement_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: BrineshoreTown_Rathouse_Basement_OnTransition
}

script BrineshoreTown_Rathouse_Basement_OnTransition{
    setrespawn(HEAL_LOCATION_BRINESHORE_TOWN)
    
    setflag(FLAG_TEMP_1)
    setflag(FLAG_TEMP_2)

    if(!flag(FLAG_RAT_BASEMENT_QUEST)){
        setvar(VAR_TEMP_0, 1)
    }
}

script BasementRatBattleIntro{
    lockall
    clearflag(FLAG_TEMP_2)
    addobject(2)
    applymovement(2, YewWalkRatBasement1)
    waitmovement(2)
    msgbox("{COLOR BLUE}Assistant Yew: {COLOR DARK_GRAY} Hmmm, this is quite\nstrange...", MSGBOX_NPC)
    waitmessage
    applymovement(2, YewWalkRatBasement2)
    waitmovement(2)
    msgbox("{COLOR BLUE}Assistant Yew: {COLOR DARK_GRAY}It sounds very lively down there,\nhow deep could it go?", MSGBOX_NPC)
    waitmessage

    clearflag(FLAG_TEMP_1)
    addobject(1)

    applymovement(1, RatMovement1)
    waitmovement(1)
    applymovement(OBJ_EVENT_ID_PLAYER, PlayerProtectYew)
    applymovement(1, RatMovement2)

    call(BasementRatBattle)

    releaseall
}

script BasementRatBattle{
    setwildbattle(SPECIES_RATTATA, 8, ITEM_ORAN_BERRY, SPECIES_RATTATA_ALOLAN, 8, ITEM_ORAN_BERRY)
    settotemboost(B_POSITION_OPPONENT_RIGHT, 1, 0, 1)
    playmoncry(SPECIES_RATTATA, CRY_MODE_ENCOUNTER)
    dowildbattle

    specialvar(VAR_RESULT, GetBattleOutcome)

    if(var(VAR_RESULT) == B_OUTCOME_CAUGHT){
        fadescreen(FADE_TO_BLACK)
        setflag(FLAG_TEMP_1)
        removeobject(1)
        setflag(FLAG_RAT_BASEMENT_QUEST)
        fadescreen(FADE_FROM_BLACK)

        msgbox("{COLOR BLUE}Assistant Yew: {COLOR DARK_GRAY}Thank you, I don't know\nwhat would have happened without you.", MSGBOX_AUTOCLOSE)
    }
    elif(var(VAR_RESULT) == B_OUTCOME_WON){
        fadescreen(FADE_TO_BLACK)
        setflag(FLAG_TEMP_1)
        removeobject(1)
        setflag(FLAG_RAT_BASEMENT_QUEST)
        fadescreen(FADE_FROM_BLACK)
        msgbox("{COLOR BLUE}Assistant Yew: {COLOR DARK_GRAY}Thank you, I don't know\nwhat would have happened without you.", MSGBOX_AUTOCLOSE)
    }
    elif(var(VAR_RESULT) == B_OUTCOME_RAN){
        warpwhitefade(MAP_BRINESHORE_TOWN, 29, 23)
    }

    setvar(VAR_TEMP_0, 0)
}

movement YewWalkRatBasement1{
    emote_exclamation_mark
    walk_up * 1
    face_right
}

movement YewWalkRatBasement2{
    walk_up * 2
    walk_left * 1
}

movement RatMovement1{
    walk_fast_down
    walk_fast_right
    walk_fast_up
}

movement RatMovement2{
    walk_fast_up * 2
    walk_fast_left * 2
}

movement PlayerProtectYew{
    emote_double_exclamation_mark
    walk_fast_up * 3
}