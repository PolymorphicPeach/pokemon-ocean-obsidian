mapscripts GildedHollow_1F_MapScripts{
    MAP_SCRIPT_ON_TRANSITION: GildedHollow_1F_OnTransition
}

script GildedHollow_1F_OnTransition{
    setrespawn(HEAL_LOCATION_ROUTE_1)
    setflag(FLAG_ROBERT_JOINED_PARTY)
    
    // remove following line when continuing development
    if(!flag(FLAG_ROBERT_JOINED_PARTY)){
        setvar(VAR_TEMP_0, 1)
        setflag(FLAG_TEMP_2)

        setflag(FLAG_TEMP_3)
        setflag(FLAG_TEMP_4)
        setflag(FLAG_TEMP_5)
        setflag(FLAG_TEMP_6)
    }
}

script GH_Hiker{
    trainerbattle_double(GH_HIKER, HikerIntro, HikerOutro, NeedPokemon)
    msgbox(HikerStandard, MSGBOX_NPC)
}

script GH_CoolTrainer{
    trainerbattle_double(GH_COOLTRAINER, CoolTrainerIntro, CoolTrainerOutro, NeedPokemon)

    msgbox(CoolTrainerStandard, MSGBOX_NPC)
    applymovement(7, face_away_player)

    waitmovement(7)

    msgbox(PushHimText, MSGBOX_YESNO)


    if(var(VAR_RESULT) == YES){
        applymovement(7, PushPerson)
        playse(SE_FALL)
        waitmovement(7)
        setflag(FLAG_GH_PUSHED_PERSON)
        removeobject(7)
        playse(SE_M_SELF_DESTRUCT)
    }
    release
}

script GH_Ranger{
    trainerbattle_double(GH_RANGER, RangerIntro, RangerOutro, NeedPokemon)
    msgbox(RangerStandard, MSGBOX_NPC)
}

script GH_Psychic1{
    trainerbattle_double(GH_PSYCHIC1, Psychic1Intro, Psychic1Outro, NeedPokemon)
    msgbox(Psychic1Standard, MSGBOX_NPC)
}

script GH_Psychic2{
    trainerbattle_double(GH_PSYCHIC2, Psychic2Intro, Psychic2Outro, NeedPokemon)
    msgbox(Psychic2Standard, MSGBOX_NPC)
}

// Obj Ids
// Psychic3: 14
// Psychic4: 15
// Psychic5: 16
// Psychic6: 17

// Robert: 18
// Yew: 19


script RecruitRobert{
    lock
    call(YewRobertArgument)
    call(PsychicsEnter)
    call(RobertHealParty)
    call(NorthWestPsychicBattle)

    release
}

script YewRobertArgument{
    applymovement(OBJ_EVENT_ID_PLAYER, emote_exclamation_mark)
    applymovement(18, RobertRunLeft)
    waitmovement(18)
    playse(SE_BANG)
    applymovement(18, RobertRecoilRight)
    waitmovement(18)

    clearflag(FLAG_TEMP_2)
    addobject(19)

    applymovement(19, YewMoveLeft)
    waitmovement(19)
    applymovement(OBJ_EVENT_ID_PLAYER, face_left)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(YewComplains, MSGBOX_DEFAULT)
    waitmessage

    applymovement(OBJ_EVENT_ID_PLAYER, face_right)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(RobertAskForHelp, MSGBOX_DEFAULT)
    waitmessage

    applymovement(OBJ_EVENT_ID_PLAYER, face_left)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(YewResponse, MSGBOX_DEFAULT)
    waitmessage
}

script PsychicsEnter{
    special(SpawnCameraObject)
    fadenewbgm(MUS_B_PYRAMID_TOP)
    
    // South Psychic
    clearflag(FLAG_TEMP_3)
    addobject(16)
    playse(SE_M_TELEPORT)
    applymovement(OBJ_EVENT_ID_CAMERA, CameraSouth)
    applymovement(16, fly_down)
    waitmovement(16)
    applymovement(18, face_down)
    waitmovement(18)
    applymovement(19, face_down)
    waitmovement(19)
    applymovement(OBJ_EVENT_ID_PLAYER, face_down)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(PsychicSouthIntro, MSGBOX_DEFAULT)
    waitmessage

    // North Psychic
    applymovement(OBJ_EVENT_ID_CAMERA, CameraNorth)
    clearflag(FLAG_TEMP_5)
    addobject(15)
    playse(SE_M_TELEPORT)
    applymovement(15, fly_down)
    waitmovement(15)
    applymovement(18, face_up)
    waitmovement(18)
    applymovement(19, face_up)
    waitmovement(19)
    applymovement(OBJ_EVENT_ID_PLAYER, face_up)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(PsychicNorthIntro, MSGBOX_DEFAULT)
    waitmessage

    // West Psychic
    applymovement(OBJ_EVENT_ID_CAMERA, CameraWest)
    clearflag(FLAG_TEMP_4)
    addobject(14)
    playse(SE_M_TELEPORT)
    applymovement(14, fly_down)
    waitmovement(14)
    applymovement(18, face_left)
    waitmovement(18)
    applymovement(19, face_left)
    waitmovement(19)
    applymovement(OBJ_EVENT_ID_PLAYER, face_left)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(PsychicWestIntro, MSGBOX_DEFAULT)
    waitmessage

    // East Psychic
    applymovement(OBJ_EVENT_ID_CAMERA, CameraEast)
    clearflag(FLAG_TEMP_6)
    addobject(17)
    playse(SE_M_TELEPORT)
    applymovement(17, fly_down)
    waitmovement(17)
    applymovement(18, face_right)
    waitmovement(18)
    applymovement(OBJ_EVENT_ID_PLAYER, face_down)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(PsychicEastIntro, MSGBOX_DEFAULT)
    waitmessage

    applymovement(OBJ_EVENT_ID_CAMERA, CameraReset)
    waitmovement(OBJ_EVENT_ID_CAMERA)

    fadeinbgm(2)
    special(RemoveCameraObject)
}

script RobertHealParty{
    applymovement(18, face_left)
    waitmovement(18)
    msgbox(RobertOffersHealing, MSGBOX_DEFAULT)
    waitmessage
    special(HealPlayerParty)
    playfanfare(MUS_RG_HEAL)
    msgbox("{COLOR BLUE}Your party has been fully healed.", MSGBOX_DEFAULT)
    waitmessage
}

script NorthWestPsychicBattle{
    applymovement(14, PsychicWestMoveIn)
    applymovement(15, PsychicNorthMoveIn)
    waitmovement(15)
    // multi_fixed_2_vs_2(TRAINER_WALLACE, text1test, TRAINER_SIDNEY, text2test, PARTNER_STEVEN, TRAINER_BACK_PIC_STEVEN)
    multi_fixed_2_vs_2(GH_PSYCHIC3, Psychic3BattleIntro, GH_PSYCHIC4, Psychic4BattleIntro, PARTNER_YEW1, TRAINER_BACK_PIC_STEVEN)
}

script SouthEastPsychicBattle{
    multi_fixed_2_vs_2(GH_PSYCHIC5, Psychic5BattleIntro, GH_PSYCHIC6, Psychic6BattleIntro, PARTNER_ROBERT1, TRAINER_BACK_PIC_ROBERT)
}

text(local) HikerIntro{
    "My family used to work in here.\n"
    "I wanted to see this place for myself."
}

text(local) HikerOutro{
    "Thank you, the battle was\n"
    "a nice distraction."
}

text(local) HikerStandard{
    "My father and his father died of\n"
    "black-lung because of this old mine"
}

text(local) CoolTrainerIntro {
    "AAAAAAHHHH!!!"
}

text(local) CoolTrainerOutro{
    "Sorry, I thought you were trying to\n"
    "push me!"
}

text(local) CoolTrainerStandard{
    "Hey again! Cool place, huh?\n"
    "I wonder how far down it goes."
}

text(local) PushHimText{
    "{COLOR RED}Push him?"
}

text(local) RangerIntro{
    "Excuse me, this place is off-limits\n"
    "by decree of the Pokemon League."
}

text(local) RangerOutro{
    "Ugh whatever, just don't fall or\n"
    "anything... I hate this job."
}

text(local) RangerStandard{
    "You'd really be doing me a favor if\n"
    "you could please leave."
}

text(local) Psychic1Intro{
    "Hmmm, could you be him?"
}

text(local) Psychic1Outro{
    "You're not him."
}

text(local) Psychic1Standard{
    "I'm going to use my psychic powers to\n"
    "find some gold!"
}

text(local) Psychic2Intro{
    "I'm supposed to be looking for someone,\n"
    "but I'd rather not..."
}

text(local) Psychic2Outro{
    "Yay, now I can just hang out. Thanks!"
}

text(local) Psychic2Standard{
    "I'm going to stay here and practice my\n"
    "dancing for a bit. I'm paid by the hour."
}

text(local) PsychicSouthIntro{
    "That's too bad {COLOR BLUE}Robert {COLOR DARK_GRAY}; it looks like\n"
    "you'll be coming with us."
}

text(local) PsychicNorthIntro{
    "The {COLOR RED}Grand Psychopomp {COLOR DARK_GRAY}does not wish\n"
    "you harm, please come peacefully."
}

text(local) PsychicWestIntro{
    "I want to try out my new Pokemon.\n"
    "Please, choose violence."
}

text(local) PsychicEastIntro{
    "I agree, our Pokemon need to be\n"
    "battle-hardened to become stronger.\p"
    "New plan, we take {COLOR BLUE}Robert {COLOR DARK_GRAY}and toss\n"
    "the witnesses over."
}

text(local) RobertOffersHealing{
    "{COLOR BLUE}Robert: {COLOR DARK_GRAY}Quick, let me heal your party!"
}

text(local) Psychic3BattleIntro{
    "{COLOR RED}Hunter: {COLOR DARK_GRAY}You really shouldn't have\n"
    "kept hiding for so long."
}

text(local) Psychic4BattleIntro{
    "{COLOR RED}Dalton: {COLOR DARK_GRAY}These things better evolve\n"
    "soon."
}

text(local) Psychic5BattleIntro{
    ""
}

text(local) Psychic6BattleIntro{
    ""
}

script TalkToBlocker{
    msgbox(blockerText, MSGBOX_NPC)
}

script TalkToDusclops{
    msgbox(dusclopsText, MSGBOX_NPC)
}

// ======== Temporary blockers =============
text(local) blockerText{
    "There's no content beyond this point\n"
    "right now. Thanks for playing!"
}

text(local) dusclopsText{
    "Dusclops!"
}
// ------------------------------------------

movement(local) CameraSouth{
    walk_fast_down * 5
}

movement(local) CameraNorth{
    walk_fast_up * 8
    walk_fast_left
}

movement(local) CameraWest{
    walk_fast_down * 5
    walk_fast_left
}

movement(local) CameraEast{
    walk_fast_right * 4
    walk_fast_up
}

movement(local) CameraReset{
    walk_fast_up
    walk_fast_left * 2
}

movement face_left{
    face_left
}

movement(local) face_away_player{
    face_away_player
}

movement(local) PushPerson{
    jump_2_right
}

movement(local) RobertRunLeft{
    walk_fast_up * 2
    walk_fast_left * 5
    walk_fast_up * 4
    walk_fast_left
}

movement(local) RobertRecoilRight{
    jump_special_right
    walk_left
}

movement(local) YewMoveLeft{
    walk_fast_left
    face_right
}

movement emote_exclamation_mark{
    emote_exclamation_mark
}

movement(local) fly_down{
    fly_down
}

movement(local) PsychicNorthMoveIn{
    walk_slow_down * 2
}

movement(local) PsychicWestMoveIn{
    walk_slow_up * 2
}

text(local) YewComplains{
    "{COLOR BLUE}Yew: {COLOR DARK_GRAY}Hey watch it! This is no place\n"
    "to be running around like that."
}

text(local) RobertAskForHelp{
    "{COLOR BLUE}???: {COLOR DARK_GRAY}I know, I know! Listen, there are\n"
    "people following me. I need help."
}

text(local) YewResponse{
    "{COLOR BLUE}Yew: {COLOR DARK_GRAY}Sorry, we don't have time... \n"
    "{PLAYER} and I have places\p"
    "to be and Pokemon to catch."
}